%import common.INT -> INT
%import common.CNAME -> NAME
%import common.WS

%ignore WS

?start: exp

?exp: seq_exp

?seq_exp: assign_exp ";" seq_exp -> seq
        | assign_exp

?assign_exp: NAME ":=" if_exp -> assign
           | if_exp


?if_exp: "if" if_exp "then" if_exp "else" if_exp -> if_
       | or_exp

?or_exp: or_exp "||" and_exp -> or_
       | and_exp

?and_exp: and_exp "&&" not_exp -> and_
        | not_exp

?not_exp: "!" not_exp -> not_
        | comp_exp

?comp_exp: join_exp "==" join_exp -> eq
         | join_exp "!=" join_exp -> neq
         | join_exp "<" join_exp -> lt
         | join_exp ">" join_exp -> gt
         | join_exp "<=" join_exp -> leq
         | join_exp ">=" join_exp -> geq
         | join_exp

?join_exp: arith_exp1 "|" join_exp -> join  //= DOMAIN =//
         | arith_exp1

?arith_exp1: arith_exp1 "+" arith_exp2 -> add
           | arith_exp1 "-" arith_exp2 -> sub
           | arith_exp2

?arith_exp2: arith_exp2 "*" neg_exp -> mul
           | arith_exp2 "/" neg_exp -> div
           | neg_exp

?neg_exp: "-" neg_exp -> neg
        | slice_exp

?slice_exp: slice_exp "[" atom ":" atom "]" -> slice  //= DOMAIN =//
          | atom

?atom: INT -> int
     | NAME -> name
     | "(" NAME "," INT ")"-> note  //= DOMAIN =//
     | "(" exp ")"
     | "let" NAME "=" exp "in" exp "end" -> let
     | "letfun" NAME "(" NAME ")" "=" exp "in" exp "end" -> letfun
     | atom "(" exp ")" -> app
